<html lang="en">
	<head>
		<title> </title>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">

		<style>
		@font-face {
			font-family: 'Value-Serif';
			src: url('/fonts/Value-Serif/Value-Serif.eot?#iefix') format('embedded-opentype'),  url('Value-Serif.woff') format('woff'), url('Value-Serif.ttf')  format('truetype'), url('Value-Serif.svg#Value-Serif') format('svg');
			font-weight: normal;
			font-style: normal;
		}
		html {
			width: 100%;
			height: 100%;
			background: #B8C6DB; /* Old browsers */ #DAD2F3

			/* B8C6DB to 000000 or 0 opacity */
			background: -moz-linear-gradient(top,  #B8C6DB 0%, #000000 100%); /* FF3.6-15 */
			background: -webkit-linear-gradient(top,  #B8C6DB 0%,#000000 100%); /* Chrome10-25,Safari5.1-6 */
			background: linear-gradient(to bottom,  #B8C6DB 0%,#000000 100%); /* W3C, IE10+, FF16+, Chrome26+, Opera12+, Safari7+ */
			filter: progid:DXImageTransform.Microsoft.gradient( startColorstr='#B8C6DB', endColorstr='#000000',GradientType=0 ); /* IE6-9 */
		}
		body{
			width: 1920px;
			height: 1080px;
			position: relative;
		}
		.expression{
			font-family: 'Value-Serif' !important;
			position: absolute;
			margin: auto;
			display: block;
			width: 100%;
		}
		#hypotitle{
			color: #ffff00;
			/* top: .5em; */
			bottom: 1em;
			font-size: 2em;
			text-align: center;
		}
		#subtitle{
			color: #ff0000;
			top: 50%;
			font-size: 1.5em;
			left: 1em;
			text-align: left;
			width: 25%;
			min-width: 400px;
		}
		#subtitle2{
			color: #0000ff;
			top: 50%;
			font-size: 1.5em;
			right: 1em;
			text-align: right;
			width: 25%;
			min-width: 400px;
		}


		</style>
	</head>
	<body>
		<!-- Fragment shader for protoplanet's position -->
		<script id="computeShaderPosition" type="x-shader/x-fragment">
			#define delta ( 1.0 / 60.0 )
			void main() {
				vec2 uv = gl_FragCoord.xy / resolution.xy;
				vec4 tmpPos = texture2D( texturePosition, uv );
				vec3 pos = tmpPos.xyz;
				vec4 tmpVel = texture2D( textureVelocity, uv );
				vec3 vel = tmpVel.xyz;
				float mass = tmpVel.w;
				if ( mass == 0.0 ) {
					vel = vec3( 0.0 );
				}
				// Dynamics
				pos += vel * delta;
				gl_FragColor = vec4( pos, 1.0 );
			}
		</script>

		<!-- Fragment shader for protoplanet's velocity -->
		<script id="computeShaderVelocity" type="x-shader/x-fragment">
			// For PI declaration:
			#include <common>
			#define delta ( 1.0 / 60.0 )
			uniform float gravityConstant;
			uniform float density;
			const float width = resolution.x;
			const float height = resolution.y;
			float radiusFromMass( float mass ) {
				// Calculate radius of a sphere from mass and density
				return pow( ( 3.0 / ( 4.0 * PI ) ) * mass / density, 1.0 / 3.0 );
			}
			void main()	{
				vec2 uv = gl_FragCoord.xy / resolution.xy;
				float idParticle = uv.y * resolution.x + uv.x;
				vec4 tmpPos = texture2D( texturePosition, uv );
				vec3 pos = tmpPos.xyz;
				vec4 tmpVel = texture2D( textureVelocity, uv );
				vec3 vel = tmpVel.xyz;
				float mass = tmpVel.w;
				if ( mass > 0.0 ) {
					float radius = radiusFromMass( mass );
					vec3 acceleration = vec3( 0.0 );
					// Gravity interaction
					for ( float y = 0.0; y < height; y++ ) {
						for ( float x = 0.0; x < width; x++ ) {
							vec2 secondParticleCoords = vec2( x + 0.5, y + 0.5 ) / resolution.xy;
							vec3 pos2 = texture2D( texturePosition, secondParticleCoords ).xyz;
							vec4 velTemp2 = texture2D( textureVelocity, secondParticleCoords );
							vec3 vel2 = velTemp2.xyz;
							float mass2 = velTemp2.w;
							float idParticle2 = secondParticleCoords.y * resolution.x + secondParticleCoords.x;
							if ( idParticle == idParticle2 ) {
								continue;
							}
							if ( mass2 == 0.0 ) {
								continue;
							}
							vec3 dPos = pos2 - pos;
							float distance = length( dPos );
							float radius2 = radiusFromMass( mass2 );
							if ( distance == 0.0 ) {
								continue;
							}
							// Checks collision
							if ( distance < radius + radius2 ) {
								if ( idParticle < idParticle2 ) {
									// This particle is aggregated by the other
									vel = ( vel * mass + vel2 * mass2 ) / ( mass + mass2 );
									mass += mass2;
									radius = radiusFromMass( mass );
								}
								else {
									// This particle dies
									mass = 0.0;
									radius = 0.0;
									vel = vec3( 0.0 );
									break;
								}
							}
							float distanceSq = distance * distance;
							float gravityField = gravityConstant * mass2 / distanceSq;
							gravityField = min( gravityField, 1000.0 );
							acceleration += gravityField * normalize( dPos );
						}
						if ( mass == 0.0 ) {
							break;
						}
					}
					// Dynamics
					vel += delta * acceleration;
				}
				gl_FragColor = vec4( vel, mass );
			}
		</script>

		<!-- Particles vertex shader -->
		<script type="x-shader/x-vertex" id="particleVertexShader">
			// For PI declaration:
			#include <common>
			uniform sampler2D texturePosition;
			uniform sampler2D textureVelocity;
			uniform float cameraConstant;
			uniform float density;
			varying vec4 vColor;
			float radiusFromMass( float mass ) {
				// Calculate radius of a sphere from mass and density
				return pow( ( 3.0 / ( 4.0 * PI ) ) * mass / density, 1.0 / 3.0 );
			}
			void main() {
				vec4 posTemp = texture2D( texturePosition, uv );
				vec3 pos = posTemp.xyz;
				vec4 velTemp = texture2D( textureVelocity, uv );
				vec3 vel = velTemp.xyz;
				float mass = velTemp.w;
				vColor = vec4( 1.0, mass / 250.0, 0.0, 1.0 );
				vec4 mvPosition = modelViewMatrix * vec4( pos, 1.0 );
				// Calculate radius of a sphere from mass and density
				//float radius = pow( ( 3.0 / ( 4.0 * PI ) ) * mass / density, 1.0 / 3.0 );
				float radius = radiusFromMass( mass );
				// Apparent size in pixels
				if ( mass == 0.0 ) {
					gl_PointSize = 0.0;
				}
				else {
					gl_PointSize = radius * cameraConstant / ( - mvPosition.z );
				}
				gl_Position = projectionMatrix * mvPosition;
			}
		</script>

		<!-- Particles fragment shader -->
		<script type="x-shader/x-fragment" id="particleFragmentShader">
			varying vec4 vColor;
			void main() {
				float f = length( gl_PointCoord - vec2( 0.5, 0.5 ) );
				if ( f > 0.5 ) {
					discard;
				}
				gl_FragColor = vColor;
			}
		</script>
    <div id="container"></div>

		<p id="hypotitle" class="expression"> </p>
		<p id="subtitle" class="expression"> </p>
		<p id="subtitle2" class="expression"> </p>

    <script src="https://s3-us-west-2.amazonaws.com/s.cdpn.io/53148/TweenLite.min.js"></script>
		<script src="https://cdnjs.cloudflare.com/ajax/libs/gsap/1.18.4/easing/EasePack.min.js"></script>
    <script type="text/javascript" src="trans_influencer_glacier_bundle.js"></script>

  </body>
</html>
